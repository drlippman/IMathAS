PACKAGE DESCRIPTION


START LIBRARY
ID
1
UID
1143581427433
LASTMODDATE
1227157541
NAME
Examples
PARENT
0

START LIBRARY ITEMS
LIBID
1
QSETIDS
0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34

START QUESTION
QID
0

UQID
11435814263779

LASTMOD
1227157541

DESCRIPTION
Example of function type

AUTHOR
Lippman, David

CONTROL
$a,$b = nonzerodiffrands(-8,8,2)
//when using functions, define the variables in the
//function, including letter constants.  Defaults to x
$variables = "x"

//this defines the domain on which the correct and given
//answers will be compared.  Defaults to -5 to 5
$domain = "-5,5"

//this prepends the answer box with a string.
$ansprompt = "Ans="

$p = $a - $b;
//the answer is a function, as a string
$answer = "x^($p)"
$showanswer = "`x^$a/x^$b = x^($a-($b)) = x^$p" if ($p>0)
$np = -1*$p
$showanswer = "`x^$a/x^$b = x^($a-($b)) = x^$p = 1/x^$np" if ($p<0)

//to force a specific format, use $requiretimes.
//in this case, it is requiring "^" to appear less than
//twice, and "-" to appear no times.  This is preventing
//unsimplified answers and negative exponents
$requiretimes = "^,<2,-,=0,x,=1"

QCONTROL


QTYPE
numfunc

QTEXT
Simplify `x^$a/x^$b`
<BR>
Write your answer with positive exponents only.

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF
yahoo.com

LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
1

UQID
11435814263780

LASTMOD
1227157541

DESCRIPTION
Example of Matching type 2

AUTHOR
Lippman, David

CONTROL
$qarr = array("`sin x`","`cos x`","`x^2`","`x^3`","`e^x`","`log x`","`2^x`")
$aarr = array("`cos x`","`-sin x`","`2x`","`3x^2`","`e^x`","`1/x`","`2^x ln2`")

//this jointly shuffles the arrays (retaining respective
//pairing), and picks 4 elements of the $qarr, and 5
//elements of the $aarr.
$questions,$answers = jointshuffle($qarr,$aarr,4,5)

//this titles the list of questions and answers
$questiontitle = "`f(x)`"
$answertitle = "`f'(x)`"

QCONTROL


QTYPE
matching

QTEXT
Match each function with it's derivative.

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
2

UQID
11435814263782

LASTMOD
1227157541

DESCRIPTION
Example of calculated matrix type

AUTHOR
Lippman, David

CONTROL
//if $answersize is specified, the user will be provided
//an array of boxes in which to enter their answer.  If
//not, they will be required to use ASCIIMath format for
//their answer, like ((1,2,3),(4,5,6))
$answersize="2,3"

//load in matrix functions
loadlibrary("matrix")
//form the matrices
$a = rands(1,9,6)
$ma = matrix($a,2,3)
$b = rands(1,9,6)
$mb = matrix($b,2,3)

//display forms
$mad = matrixformat($ma)
$mbd = matrixformat($mb)

//calculate the answer
$mans = matrixsum($ma,$mb)
$answer = matrixformat($mans)

QCONTROL


QTYPE
calcmatrix

QTEXT
Add: `$mad + $mbd`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
3

UQID
11435814263784

LASTMOD
1227157541

DESCRIPTION
Example of using loadlibrary to access functions in a macro file (mean from stats library in this case)

AUTHOR
Lippman, David

CONTROL
//this loads the stats library.  Admins can load new macro
//libraries to extend IMathAS.  The Macro Library Help link
//will show what libraries are currently installed.
loadlibrary("stats")

$a = nonzerodiffrands(1,10,5)

//this is a standard display macro that creates
//a table display of the array $a with title "x"
$table = showarrays("x",$a)

//here we are using the mean function from the stats
//library to determine the answer
$answer = mean($a)

QCONTROL


QTYPE
number

QTEXT
Find `bar x`

$table

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
4

UQID
11435814263786

LASTMOD
1227157541

DESCRIPTION
Example of Matching type

AUTHOR
Lippman, David

CONTROL
$a,$b,$c = rands(-3,3,3)

//shuffles the list of colors
$cols = singleshuffle("red,green,blue")

//this uses the showplot macro.  The first argument is a
//single function or an array of functions.  In this case,
//we're only specifying the function and the color.  There
//are other options available.
$graphs = array("$a*x^2+$b*x+$c,$cols[0]","2*$a*x+$b,$cols[1]","$a*x^3/3+$b*x^2/2+$c*x,$cols[2]")

//these set titles for the list of questions and answers
$questiontitle = "Function"
$answertitle = "Graph Color"

//this actually calls the showplot macro.  After the
//function, the window is specified, then the
//labels are off, and grid is set to false
$plot = showplot($graphs,-3,3,-5,5,off,false)

//this defines the questions and answers.  Note that they
//are matched - the first entry in $answers is the answer
//the the first entry in $questions.
$answers[0] = "<span class=white style='color:$cols[0]'>$cols[0]</span>"
$answers[1] = "<span class=white style='color:$cols[1]'>$cols[1]</span>"
$answers[2] = "<span class=white style='color:$cols[2]'>$cols[2]</span>"
$questions = array("`f(x)`","`f'(x)`","`int f(x)dx`")

QCONTROL


QTYPE
matching

QTEXT
Match each function with its graph

$plot

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
5

UQID
114358142637105

LASTMOD
1227157541

DESCRIPTION
Example of multipart

AUTHOR
Lippman, David

CONTROL
//this defines that there will be two parts, both of
//type calculated.  Refer the help for valid anstypes.
$anstypes = array("calculated","calculated")
$a,$b = nonzerodiffrands(-8,8,2)
$c = nonzerorand(-30,30)

$question = makeprettydisp("$a x + $b y=$c")

//in some multipart questions, it might be useful to hide
//the preview button usually provided with calculated and
//function answer types.  You can set $hidepreview to
//hide the preview button.  Note that it is suffixed with
//a [1].  This specifies to apply the option to the
//second calculated type.  All options must be suffixed
//like this in a multipart problem.
$hidepreview[1] = true

//Note the use of the $answerbox in the question text.  This places the
//answerboxes in the problem text.  Make sure you put the
//boxes in numerical order; entry tips are given assuming
//this.

//like with other options, the $answer also needs to be
//suffixed with the question part.
$answer[0] = $c/$a
$answer[1] = $c/$b

QCONTROL


QTYPE
multipart

QTEXT
Find the x and y intercepts of $question

x-int: `x=`$answerbox[0]

y-int: `y=`$answerbox[1]

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
6

UQID
114358142637107

LASTMOD
1227157541

DESCRIPTION
Example of Number type.

AUTHOR
Lippman, David

CONTROL
//a nonzero random number between -5 and 5
$a = nonzerorand(-5,5)

//a decimal number between -5 and 5, with one decimal
//we're going to ensure that $a and $b are different signs
//using the "if" conditional
$b = rrand(.1,5,.1) if ($a < 0)
$b = rrand(-5,-.1,.1) if ($a > 0)

//two different, nonzero integers
$c,$d = nonzerodiffrands(-5,5,2)

QCONTROL
//this could show up as:  -4 + -2.3 + 3 + -1
//the backquotes (`) tell it to display as math
$prob = "`$a + $b + $c + $d`";

//if we want to simplify it like: -4 - 2.3 + 3 - 1
$prob2 = makeprettydisp("$a + $b + $c + $d")

QTYPE
number

QTEXT
Find: $prob

or equivalently: $prob2

ANSWER
//for number, we just need to specify the answer.  No
//quotes here because we're calculting, not creating
//a display string
$answer = $a + $b + $c + $d

//by default, numbers are allowed a .001 relative error.
//$reltolerance = .0001 would require a higher accuracy
//$abstolerance = .01 would require an absolute error
//   under .01
//$answer = "[-10,8)" would accept any answer where
// -10 <= givenanswer < 8

SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
7

UQID
114358142637108

LASTMOD
1227157541

DESCRIPTION
Example of calculated type.

AUTHOR
Lippman, David

CONTROL
//choose two numbers from a list.  Can also choose from
//an array
$a,$b = randsfrom("2,3,5,7,11",2)

//the "where" statement is used with randomizers. It allows
//you to avoid a specific case.  In this case, we requiring
//that $a not divide evenly into $c.
$c = rand(1,10) where ($c % $a != 0)
$d = rand(1,10) where ($d % $b != 0)

//note that the student could enter 2/5*6/7 and get the
//correct answer.  We can prevent this by adding this line
$answerformat = "reducedfraction"

//like with the Number type, we supply a number as the
//answer.  The only difference is that the student can
//enter a calculation instead of a number
$answer = $c/$a * $d/$b

//to get the answer to display as a fraction rather than a
//decimal, we need to define $showanswer
$showanswer = dispreducedfraction($c*$d,$a*$b)

QCONTROL


QTYPE
calculated

QTEXT
Multiply: `$c/$a * $d/$b`

Enter your answer as a single, reduced fraction

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
8

UQID
114358142637109

LASTMOD
1227157541

DESCRIPTION
Example of multiple-choice

AUTHOR
Lippman, David

CONTROL
//pick two different nonzero numbers that don't add to 0
//the numbers are important here to ensure that all the
//choices will be different.
$a,$b = nonzerodiffrands(-5,5,2)

//we can either define the entire $questions array
//at once, or define each piece separately.  The former
//would look like:  $questions = array($a+$b,$a-$,...
$questions[0] = $a+$b
$questions[1] = $a-$b
$questions[2] = $a*$b

//this will lay out the choices horizontally.  To do
//a standard vertical layout, just omit this line
$displayformat = "horiz"

$text = makeprettydisp("$a+$b")

//Here the answer is the INDEX into the questions array
//that holds the correct answer.  Arrays are zero-indexed,
//so the first entry is at index 0.
$answer = 0

QCONTROL


QTYPE
choices

QTEXT
Find $text

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
9

UQID
114358142637110

LASTMOD
1227157541

DESCRIPTION
Example of Multiple-Answer type

AUTHOR
Lippman, David

CONTROL
//the $questions array is a list of the options.
//The listtoarray macro converts a list of numbers or
//strings to an array.  Use calclisttoarray to convert
//a list of calculations to an array of numbers
$questions = listtoarray("`sin(x)`,`sin^-1(x)`,`tan(x)`,`csc(x)`,`x^2`");

//the answer here is a list of indexes into the $questions
//array that contain correct answers.  Remember that
//arrays are 0-indexed
$answers = "0,2,3"

//Normally, each part is given equal weight.  If you wish
//to divide the point score only by the number of correct
//answers, use this line:
//$scoremethod = "answers"

QCONTROL


QTYPE
multans

QTEXT
Select all the functions that are periodic

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
10

UQID
114358142637111

LASTMOD
1227157541

DESCRIPTION
Example of graph capabilities

AUTHOR
Lippman, David

CONTROL
$anstypes = listtoarray("number,number,number,number")

//for each graph, it's:
//function,color,xmin,xmax,startmark,endmark
$graphs[0] = "-x-5,black,-5,-1,,closed"
$graphs[1] = "-2x+3,black,-1,2,open"
$graphs[2] = "-2x+3,black,2,5,open"
//last one is really just a dot, but we define it as
//a function
$graphs[3] = "2,black,2,2,closed"

//graphs,xmin,xmax,ymin,ymax,labels,grid
$plot = showplot($graphs,-5,5,-5,5,1,1)

//the backslashes above add extra spacing between the
//limit and the f(x)

//this question isn't randomized
$answer[0] = 5
$answer[1] = -4
$answer[2] = "DNE"
$answer[3] = -1

QCONTROL


QTYPE
multipart

QTEXT
The graph below is the function `f(x)`

$plot

Find `lim_(x-&gt;-1^+)\ \ f(x)`  $answerbox[0]

Find `lim_(x-&gt;-1^-)\ \ f(x)`  $answerbox[1]

Find `lim_(x-&gt;-1)\ \ f(x)`  $answerbox[2]

Find `lim_(x-&gt;2)\ \ f(x)` $answerbox[3]

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
13

UQID
1141954525122484

LASTMOD
1227157541

DESCRIPTION
Solve matrix equation

AUTHOR
Lippman, David

CONTROL
loadlibrary("matrix")
$mat = matrixidentity(3)
//silly approach to guarantee a nonsingular matrix
$a,$b,$c = nonzerorands(-3,3,3)
$mat = matrixrowcombine3($mat,0,$a,1,$b,2,$c,0)
$a,$b,$c = nonzerorands(-3,3,3)
$mat = matrixrowcombine3($mat,0,$a,1,$b,2,$c,1)
$a,$b,$c = nonzerorands(-3,3,3)
$mat = matrixrowcombine3($mat,0,$a,1,$b,2,$c,2)

$x = matrix(rands(-3,7,3),3,1)
$rs = matrixprod($mat,$x)

$md = matrixformat($mat)
$rsd = matrixformat($rs)

$answer = matrixformat($x)
$answersize = "3,1"

QCONTROL


QTYPE
matrix

QTEXT
Solve: `$md \ vec x = $rsd`

`vec x` = $answerbox

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
11

UQID
1144000847040400

LASTMOD
1227157541

DESCRIPTION
plot3d example

AUTHOR
Lippman, David

CONTROL
loadlibrary("plot3d")

$graph = spacecurve("[cos(t),sin(t),t]",0,10,50)

$plot = plot3d("x^2+y^2",-2,2,-2,2)

$questions = array("A","B")
$answer = 0

QCONTROL


QTYPE
choices

QTEXT
A: $graph

B: $plot

Which of the graphs above is a spacecurve?

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
12

UQID
1151342979988855

LASTMOD
1441641234

DESCRIPTION
labeling graphs example

AUTHOR
Lippman, David, mb Deokar,Bhavana

CONTROL
$a,$b = diffrands(1,5,2)
$eqns = array(\"$a*cos(pi/2*x)\",\"$b*sin(pi/2*x)\")
$m = max($a,$b)
$plot = showplot($eqns,-3,3,-1*$m-1,$m+1)
$plot = addlabel($plot,0,$a,\"f(x)\",\"black\",\"above\")
$plot = addlabel($plot,1,$b,\"g(x)\",\"black\",\"above\")

$answer = \"$a*cos(pi/2*x)+$b*sin(pi/2*x)\"
$showanswer = makeprettydisp($answer)

QCONTROL


QTYPE
numfunc

QTEXT
$plot

Let `h(x)=f(x)+g(x)`

Write an equation for `h(x)`

`h(x)`= $answerbox

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION
lzlfasmdaldslasdlasdlasmdlamsldmasldasd

QIMGS
12345,9Assessment2.png

START QUESTION
QID
14

UQID
1159820322266971

LASTMOD
1227157541

DESCRIPTION
Linear inequalities in 2 variables

AUTHOR
Lippman, David

CONTROL
loadlibrary("ineq")

$eqns = array("-x+2,below","x^2-3,above,black,dash","x+1,below")
$graph = ineqbetweenplot($eqns ,-5,5,-5,5)

$eqns = array("-x+2,abovediag,green,black,dash","2x+1,belowdiag,blue")
$graph2 = ineqplot($eqns,-5,5,-5,5)

QCONTROL


QTYPE
calculated

QTEXT
$graph

$graph2

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
16

UQID
1168537684869012

LASTMOD
1227157541

DESCRIPTION
Add polynomials: demo of polys macro library

AUTHOR
Lippman, David

CONTROL
loadlibrary("polys")
//loads polynomial macro library.  See Macro Library Help for syntax info
$A=nonzerodiffrands(-12,12,6)
$B=nonzerodiffrands(-12,12,6)

$r1,$r2=diffrands(0,5,2)
$s1,$s2=diffrands(0,5,2)

$A[$r1]=0
$A[$r2]=0
$B[$s1]=0
$B[$s2]=0

$p1 = formpoly($A,5)
$p2 = formpoly($B,5)

$pd1 = writepoly($p1)
$pd2 = writepoly($p2)

$answer = writepoly(addpolys($p1,$p2))


$answerboxsize=40

QCONTROL


QTYPE
numfunc

QTEXT
Add the polynomials:

`($pd1)+($pd2)`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
17

UQID
1171578622923677

LASTMOD
1227157541

DESCRIPTION
Example of a Sets question

AUTHOR
Lippman, David

CONTROL
$anstypes = array("string","string","string","choices","choices")
$all = listtoarray("a,b,c,d,e,f,g,h,i,j,k,m")

$a = diffrandsfrom($all,rand(5,7))
$a = sortarray($a)
$dispa = arraytolist($a)
$b = diffrandsfrom($all,rand(5,7))
$b = sortarray($b)
$dispb = arraytolist($b)

$union = unionarrays($a,$b)
$intersect = intersectarrays($a,$b)

$strflags = "ignore_case=1,remove_whitespace=1,ignore_order=1,ignore_commas=1"

$answer[0] = arraytolist($union)
$answer[1] = arraytolist($intersect)
$answer[2] = arraytolist(diffarrays($union,$intersect))

$r = rand(0,1)
$questions[3] = array("A","B","both A and B")
$answer[3]= $r
$elinset = randfrom(diffarrays($a,$intersect)) if ($r==0)
$elinset = randfrom(diffarrays($b,$intersect)) if ($r==1)
//this is failsafe if the intersection = the set
$answer[3] = 2 if ($elinset==null)
$elinset = randfrom($intersect) if ($elinset==null)

$noshuffle[3] = "all"
$displayformat[3] = "select"

$r = rand(0,1)
$subset = sortarray(diffrandsfrom($a,rand(2,3))) if ($r==0)
$subset = sortarray(diffrandsfrom($b,rand(2,3))) if ($r==1)
$dispsubset = arraytolist($subset)
//check if subset is also subset of other
$r = 2 if (intersectarrays($subset,$intersect)==$subset)
$questions[4] = array("A","B","both A and B")
$answer[4] = $r

$noshuffle[4] = "all"
$displayformat[4] = "select"

QCONTROL


QTYPE
multipart

QTEXT
`A = { $dispa }`<br/>
`B = { $dispb }

Enter the elements in each set, like: a,b,c

`A uu B` = { $answerbox[0] }

`A nn B` = { $answerbox[1] }

`bar(A nn B)` = { $answerbox[2] }

`$elinset in` $answerbox[3]

`{ $dispsubset } sub` $answerbox[4]

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
18

UQID
1173048553489898

LASTMOD
1227157541

DESCRIPTION
Example of using hints

AUTHOR
Lippman, David

CONTROL
$a,$b,$c = diffrands(2,10,3)

$answer = ($c-$a)/$b

$hints[1] = "Subtract $b from both sides of the equation:  `$a x = $c - $b`"
$hints[2] = $hints[1]."<br/>Divide both sides of the equation by $a:  `x = ($c-$b)/$a`"

QCONTROL


QTYPE
calculated

QTEXT
Solve: `$a x + $b = $c`

x = $answerbox

$hintloc

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
15

UQID
1173417960546095

LASTMOD
1227157541

DESCRIPTION
List of numbers example

AUTHOR
Lippman, David

CONTROL
$answerformat = "list"

$a,$b = nonzerodiffrands(-5,5,2)

loadlibrary("polys")

$p = formpoly(array(1,$a+$b,$a*$b),2)
$wp = writepoly($p)

$answer = "$a,$b"

QCONTROL


QTYPE
number

QTEXT
Solve the equation `$wp = 0`

Enter all solutions below as a list of numbers, separated with commas

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
19

UQID
1176587388361054

LASTMOD
1227157541

DESCRIPTION
linegraph example using interval library and interval answer type

AUTHOR
Lippman, David

CONTROL
loadlibrary("interval")
$ar = diffrands(-4,4,4)
$a,$b,$c,$d = sortarray($ar)
$ma,$mb,$mc,$md = rands(0,1,4) //markertypes - 0:open, 1:closed

$intervals[0] = forminterval("-oo",$a,0,$ma)
$intervals[1] = forminterval($b,$c,$mb,$mc)
$intervals[2] = forminterval($d,"oo",$md,0)

$plot = linegraph($intervals,-5,5)

$answer = joinarray($intervals,' U ')

QCONTROL


QTYPE
interval

QTEXT
$plot

Write the intervals shown above.  Use oo for `oo`, -oo for `-oo`, U for union.

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
20

UQID
1176657334258688

LASTMOD
1227157541

DESCRIPTION
Domain question using Calculated Interval

AUTHOR
Lippman, David

CONTROL
$a,$b = nonzerodiffrands(-8,8,2) where (abs($a)>1)

$inside = polymakepretty("$a x + $b")

$ep = -1*$b/$a
$answer = "(-oo,$ep]" if ($a<0)
$answer = "[$ep,oo)" if ($a>0)

QCONTROL


QTYPE
calcinterval

QTEXT
Find the domain of `f(x) = sqrt($inside)`

`x in` $answerbox

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
21

UQID
1177447790122431

LASTMOD
1227157816

DESCRIPTION
Simple showplot Graphing Tutorial

AUTHOR
Lippman, David

CONTROL
$b = nonzerorand(-4,0)
$end = rand(3,6)
$plot = showplot("2*x-$b,red,-3,2,open,closed,1,nodash",-5,$end,-15,8,2,"1:2",400,300)

//the input above graphs the following:
//SPECIFIED INSIDE THE "  ", IS INFO ON THE GRAPHED FUNCTION
//FROM LEFT TO RIGHT
//2x - $b    y=2x+$b will be graphed
//red        the line will be red
//-3,2       The domain for the graph is (-3,2].
//open       the left marker is open
//closed     the right marker is closed
//1          the pixel width of the graph is 1 (2 or higher produces thicker lines)
//undashed   the line is not dashed. leaving this blank also provides a solid line. enter the word dash for a dashed line.
//SPECIFIED OUTSIDE THE "   " BUT STILL INSIDE SHOWPLOT, IS INFO ON THE RECTANGULAR GRID.
//FROM LEFT TO RIGHT
//-5,$end      Horizontally, the grid starts at -5 and ends at the variable $end
//-15,8        Vertically, the grid starts at -15 and ends at 8
//2           means the x AND y axes has labels in increments of 2. If you want different increments, use "n:m" (see next parameter)

//"1:2"     means the x axis has gridlines in increments of 1, the y axis in increments of 2
//          Must be in quotes to specify different values.
//          A single number WITHOUT quotes produces equal labeling
//400       The graph is 400 pixels wide
//300        The graph is 300 pixels high


//addlabel(plot,x,y,"label",color,"location"])

$plot = addlabel($plot,1,1,"(1,1)",red,"above")
$plot = addlabel($plot,$end-.1,0,"t",blue,"above")
$plot = addlabel($plot,0,7.5,"Population",green,"right")

//HERE'S HOW TO ADD LABELS. EACH ONE ADDS A DIFFERENT LABLE
//FROM LEFT TO RIGHT INSIDE PARENS
//$plot   is the name of the plot you add the label to
//1,1     are the (x,y) coordinates where the label is to go
//"(1,1)"  is the actual label. Put it in quotes
//red     is the color of the label
//"above"  specifies where the label goes relative to the label's coordinates. Can be
//         "above" "below" "left" or "right"

QCONTROL


QTYPE
number

QTEXT
$plot

SEE THE CODE FOR A SOMEWHAT DETAILED BREAKDOWN OF GRAPHING FUNCTIONS

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
22

UQID
1177459717818178

LASTMOD
1227157541

DESCRIPTION
Graphics using showasciisvg; geometric figures

AUTHOR
Lippman, David

CONTROL
//set border and window
$init = "setBorder(5); initPicture(-5,5,-5,5);"

//set axes:  xscl, yscl, labels? (1 yes, null no), xgrid, ygrid
$axes = "axes(1,1,1,1,1);"

//draw a line
$line = "line([-4,1],[-1,5]);"

//draw a circle in red.  Note color changes apply to all future commands
$circ = "stroke=\"red\";circle([-4,-4],1);"

//draw an ellipse in blue
$ellip = "stroke=\"blue\";ellipse([0,-2],3,2);"

//draw a rectangle in green
$rect = "stroke=\"green\";rect([1,1],[3,2]);"

//draw a filled triangle in cyan
$tri = "fill=\"cyan\";path([[4,5],[5,3],[2,4],[4,5]]);"

//put some text in there
$a = rand(1,1000)
$text = "text([4,-2],\"Hello$a\");"

//create our graphic by concatenating all the strings we just defined
$graphic = showasciisvg($init.$axes.$line.$circ.$ellip.$rect.$tri.$text)

$answer =0

QCONTROL


QTYPE
number

QTEXT
Here's a graphic:

$graphic

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
23

UQID
1177474350101005

LASTMOD
1227157541

DESCRIPTION
Graphics using showasciisvg; plots, vectors, line thickness

AUTHOR
Lippman, David

CONTROL
//set border and window
$init = "setBorder(5); initPicture(-5,5,-5,5);"

//set axes:  xscl, yscl, labels? (1 yes, null no), xgrid, ygrid
$axes = "axes(1,1,1,1,1);"

//make a function plot: plot("function",{xmin,xmax})
$fplot = "plot(\"-x^2-2\");"

//make a parametric plot, thick
$pplot = "strokewidth=3;plot([\"2*sin(t)\",\"cos(t)\"],0,6.3);"

//draw a line with an arrow
$vector = "strokewidth=2;marker=\"arrow\";stroke=\"blue\";line([4,4],[3,2]);"

//draw a dashed line
$dash = "marker=\"none\";strokedasharray=\"5 2\";stroke=\"red\";line([-2,1],[-4,2]);"

//create our graphic by concatenating all the strings we just defined
$graphic = showasciisvg($init.$axes.$fplot.$pplot.$vector.$dash)

$answer =0

QCONTROL


QTYPE
number

QTEXT
Here's a graphic:

$graphic

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
24

UQID
1179458805125719

LASTMOD
1227157541

DESCRIPTION
Drawing entry: draw x^2-a

AUTHOR
Lippman, David

CONTROL
$a = rand(1,4)

//define grid to draw on
$grid = "-5,5,-5,5"
//define answer lines
$answers = "x^2-$a"
//limit to line entry
$answerformat = "line"

QCONTROL


QTYPE
draw

QTEXT
Sketch a graph of `y = x^2-$a`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
25

UQID
1179461229278560

LASTMOD
1227157541

DESCRIPTION
Drawing entry: draw tangent line to parabola

AUTHOR
Lippman, David

CONTROL
$a = randfrom("-.5,-.25,.25,.5")
$b = rand(1,3)
$b = -1*$b if ($a>0)

$c = nonzerorand(-2,2)

//define grid to draw on
$grid = "-5,5,-5,5"
//define background parabola
$background = "$a*x^2+$b,red"
//define answer lines:  y = mx + bb
$m = 2*$a*$c
$bb = $a*$c^2+$b - $m*$c
$answers = "$m*x+$bb"
//limit to line entry
$answerformat = "line"

QCONTROL


QTYPE
draw

QTEXT
Sketch a tangent line to the function below at `x = $c`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
27

UQID
1179462018509057

LASTMOD
1227157541

DESCRIPTION
Drawing entry: draw piecewise function

AUTHOR
Lippman, David

CONTROL
$a = rand(-3,-1)
$b = rand(1,3)

$y = rand(1,4)

//we'll use the default grid, so won't define it here
//let's define the answer functions:
$answers[0] = "0,-5,$a"    //first line
$answers[1] = "$y,$a,$b"   //second line
$answers[2] = "0,$b,5"     //third line
$answers[3] = "$a,0"       //closed dot
$answers[4] = "$a,$y,open" //open dot
$answers[5] = "$b,$y"      //closed dot
$answers[6] = "$b,0,open"  //open dot

//let's assign weights:  20% for each line segment, 10% for each dot
$partweights = array(.2,.2,.2,.1,.1,.1,.1)

QCONTROL


QTYPE
draw

QTEXT
Sketch a graph of `f(x) = {(0,"if",x le $a),($y,"if",$a lt x le $b),(0,"if",x gt $b):}`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
28

UQID
1180116746185373

LASTMOD
1227157541

DESCRIPTION
Example of essay question using editor

AUTHOR
Lippman, David

CONTROL
//enable editor - otherwise a basic text area is used
$displayformat = "editor"

//number of text rows.
$answerboxsize = "10"

//no $answer is required for essay questions
//the computer can not self-grade essay questions

QCONTROL


QTYPE
essay

QTEXT
This is an essay question.  Type your answer here.

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
26

UQID
1181541976814733

LASTMOD
1227157541

DESCRIPTION
Drawing entry: Draw a scatterplot

AUTHOR
Lippman, David

CONTROL
$x = diffrands(1,8,5)
$y = diffrands(1,8,5)
$dots = arraystodots($x,$y)

$sa = showarrays("x",$x,"y",$y)

//define grid to draw on
$grid = "-1,10,-1,10"

$answers = $dots
//limit to dot entry
$answerformat = "dot"

QCONTROL


QTYPE
draw

QTEXT
Plot these points:

$sa

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
29

UQID
1190232049844508

LASTMOD
1227157541

DESCRIPTION
Example of N-tuple (points)

AUTHOR
Lippman, David

CONTROL
$a,$b = nonzerodiffrands(-5,5,2)


$m = $a+$b
$l = $a*$b

$eqn = makepretty("x^2 - $m x + $l")

$answer = "($a,0),($b,0)"
$displayformat = "pointlist"

QCONTROL


QTYPE
ntuple

QTEXT
Find the points at which the curve `y = $eqn` crosses the x-axis

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
30

UQID
1190232225090913

LASTMOD
1227157541

DESCRIPTION
Example of Calculated N-tuple (vector)

AUTHOR
Lippman, David

CONTROL
$a,$b = nonzerodiffrands(-5,5,2)

$m = $a^2+$b^2

$showanswer = "`(: $a/sqrt($m), $b/sqrt($m) :)`"

$ux = $a/sqrt($m)
$uy = $b/sqrt($m)

$answer = "<$ux,$uy>"
$displayformat = "vector"

QCONTROL


QTYPE
calcntuple

QTEXT
Find the unit vector in the direction of `lt $a,$b gt`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
31

UQID
1191778907331724

LASTMOD
1227157541

DESCRIPTION
Drawing Entry: Mark vertex of parabola

AUTHOR
Lippman, David

CONTROL
$h,$k = nonzerodiffrands(-3,3,2)

$answers = "$h,$k"
$grid = "-5,5,-5,5"
$background = "(x-$h)^2+$k"
$answerformat = "dot"

QCONTROL


QTYPE
draw

QTEXT
Place a dot at the vertex of the parabola shown below

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
32

UQID
1193679368434747

LASTMOD
1227157541

DESCRIPTION
Stuanswers example; answer meeting a condition

AUTHOR
Lippman, David

CONTROL
$anstypes = "number,number"
$a = rand(3,9)
$as = $a^2

$answerboxsize = 2

$x = $stuanswers[$thisq][0] //the answer to this question, part 0
$y = $stuanswers[$thisq][1]

$x = 9999 if ($x===null) //define to crazy value if student didn't answer
$y = 9999 if ($y===null) //define to crazy value if student didn't answer

$answer[0] = $x+100   //by default the answer is something different than what they typed
$answer[0] = $x if (abs($x^2+$y^2 -$as) < .000001)  //if they meet our condition, set answer to what they entered

$answer[1] = $y+100
$answer[1] = $y if (abs($x^2+$y^2 -$as) < .000001)

//it's always best to define showanswer here; otherwise the wrong answers above may display as the answer
$showanswer = "Any point on the circle will be acceptable"

QCONTROL


QTYPE
multipart

QTEXT
Enter a point on the circle `x^2+y^2 = $as`

($answerbox[0],$answerbox[1])

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
33

UQID
1197605392402294

LASTMOD
1227157541

DESCRIPTION
Drawing entry: polygons

AUTHOR
Lippman, David

CONTROL
//y=a-x,  y=b-2x    a-x=b-2x    x=b-a

$b = rrand(6,10,2)
$a = rand(3,$b-1) where ($b-$a<$b/2)
$hb = $b/2
$x = $b-$a
$y = $a-$x

$grid = "-1,10,-1,10"
$answers = array("0,0","0,$a","$x,$y","$hb,0","0,0")
$answerformat = "polygon"

QCONTROL


QTYPE
draw

QTEXT
Draw the boundary to the feasible region corresponding to the constraints

`{(y+x le $a),(y+2x le $b),(x ge 0),(y ge 0):}`

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION


START QUESTION
QID
34

UQID
1206685650334340

LASTMOD
1227157541

DESCRIPTION
Graphing: axis labeling

AUTHOR
Lippman, David

CONTROL
$h = rand(50,80)
$v = rand(5,30)

$g = showplot("$h+$v*x-9.8*x^2",0,5,0,100,"1:20","1:20",300,300)

//adds border around main graph, in pixels: left, bottom, right, top
$g = addplotborder($g,55,40,5,10)

//normal addlabel function, uses x,y location relative to graph coordinates
$g = addlabel($g,2,$h+2*$v-4*9.8,"f(x)","blue","aboveright")

//new addlabelabs, uses pixel x,y location; 0,0 in bottom left
$g = addlabelabs($g,175,0,"Time (sec)","red","above")

//addlabel and addlabelabs get new argument: text rotation angle
$g = addlabelabs($g,0,170,"Height (m)","red","right",90)

QCONTROL


QTYPE
number

QTEXT
$g

ANSWER


SOLUTION


SOLUTIONOPTS
0

EXTREF


LICENSE
1

ANCESTORAUTHORS


OTHERATTRIBUTION

